<S> ::= <EL>
<EL> ::= <E> *
<E> ::= <left> | <right> | <left> "=" <right> | <ifexpr> | <while> | <function> | <component> | <class>  | <interface> | <return> | <HTML>

<left> ::= <value_definition> | <value_name> | <value_name> <array>
<right> ::= <comparison_equal> | <function_message_passing> | <array> | <json>

<comparison_equal> ::= <comparison_equal> "==" <comparison> | <comparison_equal> "!=" <comparison> | <comparison>
<comparison> ::= <comparison> "<" <expr> | <comparison> "<=" <expr> | <comparison> ">" <expr> | <comparison> ">=" <expr> | <expr>

<expr> ::= <expr> "+" <term> | <expr> "-" <term> | <term>
<term> ::= <term> "*" <factor> | <term> "/" <factor>  | <term> "%" <factor> |  <factor>
<factor> ::= "(" <right> ")" | NUM | <text>

<value_definition> ::= <type_name> <value_name>

<type_name> ::= "int" | "string" | "void" | "float"

<text> ::= """ TEXT """ | "'" TEXT "'" | """ NUM """ | "'" NUM "'" | """ """ | "'" "'"
<value_name> ::= TEXT

<ifexpr> ::= <if>
<if> ::= "if" <right> <block>
<while> ::= "while" <right> <block>
<function> ::= <value_definition> "(" <argument> ")" <block>
<function_message_passing> ::= <value_name> "(" <array_line> ")"
<argument> ::= <value_definition> "," <argument>  | <value_definition> |

<return> ::= "return" | "return" <right>

<block> ::= "{" <EL> "}"

<component> ::= "component" TEXT <block> | "component" TEXT TEXT <block>

<class> ::= "class" TEXT <block> | "class" TEXT TEXT <block>
<interface> ::= "interface" TEXT <block>

<HTML> ::= <HTML_tag_start> <HTML> <HTML_tag_end> | <HTML_tag_single> | <HTML_tag_start> TEXT <HTML_tag_end> | <HTML_tag_start> <HTML_tag_end> | <HTML_tag_start> <block> <HTML_tag_end>
<HTML_tag_start> ::= "<" TEXT <HTML_attribute> ">" | "<" TEXT ">"
<HTML_tag_end> ::= "</" TEXT ">"
<HTML_tag_single> ::= "<" TEXT <HTML_attribute> "/>" | "<" TEXT "/>"
<HTML_attribute> ::= <value_name> "=" <json> | <value_name> "=" <json> <HTML_attribute>

<json> ::= "{" <json_line> "}"
<json_line> ::= <json_line> "," <json_element> | <json_element>
<json_element> ::= <key> ":" <right>

<array> ::= <array> "[" <array_line> "]" | "[" <array_line> "]"
<array_line> ::= <array_line> "," <right> | <right>