<S> ::= <EL>
<EL> ::= <E> *
<E> ::= <left> ";" | <right> ";" | <left> "=" <right> ";" | <ifexpr> ";" | <DEFINITION> ";" | <return> ";"

<left> ::= <value_definition> | <value_name> | <value_name> <array>
<right> ::= <comparison_equal> | <function_message_passing> | <function> | <array> | <json> | <HTML>

<comparison_equal> ::= <comparison_equal> "==" <comparison> | <comparison_equal> "!=" <comparison> | <comparison>
<comparison> ::= <comparison> "<" <expr> | <comparison> "<=" <expr> | <comparison> ">" <expr> | <comparison> ">=" <expr> | <expr>

<expr> ::= <expr> "+" <term> | <expr> "-" <term> | <term>
<term> ::= <term> "*" <factor> | <term> "/" <factor>  | <term> "%" <factor> |  <factor>
<factor> ::= "(" <right> ")" | NUM | <text>

<value_definition> ::= TYPE <value_name> 

<text> ::= """ TEXT """ | "'" TEXT "'" | """ NUM """ | "'" NUM "'" | """ """ | "'" "'"
<key> ::= """ TEXT """ | "'" TEXT "'" | """ NUM """ | "'" NUM "'" | TEXT | NUM
<value_name> ::= TEXT

<ifexpr> ::= <if_while> | <if_while> <else>
<if_while> ::= "if" "(" <right> ")" <block> | "while" "(" <right> ")" <block>
<else> ::= "else" "if" "(" <right> ")" <block> <else> | "else" <block> 

<function> ::= "(" <argument> ")" "=>" <block>

<function_message_passing> ::= <value_name> "(" <array_line> ")" | <value_name> "(" ")"
<argument> ::= <value_definition> "," <argument>  | <value_definition> |

<return> ::= "return" | "return" <right>

<block> ::= "{" <EL> "}"

<DEFINITION> ::= DEFINITION TEXT <block> | DEFINITION TEXT TEXT <block>

<HTML> ::= <HTML_tag_start> <HTML> <HTML_tag_end> | <HTML_tag_single> | <HTML_tag_start> TEXT <HTML_tag_end> | <HTML_tag_start> <HTML_tag_end> | <HTML_tag_start> <block> <HTML_tag_end>
<HTML_tag_start> ::= "<" TEXT <HTML_attribute> ">" | "<" TEXT ">"
<HTML_tag_end> ::= "</" TEXT ">"
<HTML_tag_single> ::= "<" TEXT <HTML_attribute> "/>" | "<" TEXT "/>"
<HTML_attribute> ::= <value_name> "=" <block> | <value_name> "=" <block> <HTML_attribute>

<json> ::= "{" <json_line> "}"
<json_line> ::= <json_line> "," <json_element> | <json_element> |
<json_element> ::= <key> ":" <right>

<array> ::= <array> "[" <array_line> "]" | "[" <array_line> "]"
<array_line> ::= <array_line> "," <right> | <right>